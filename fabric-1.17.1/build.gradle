plugins {
    id 'fabric-loom' version '0.9-SNAPSHOT'
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_16

ext.includeMod = { Object... dependencyNotation ->
    dependencies.modImplementation dependencyNotation
    dependencies.include dependencyNotation
}

dependencies {
    embed (project(":core")) {
        exclude module: 'kotlin-stdlib-jdk8'
        exclude module: 'kotlin-reflect'
        exclude module: 'Elementa'
        exclude module: 'UniversalCraft'
    }

    includeMod ('gg.essential:Elementa:next-337-11700-fabric-SNAPSHOT') {
        exclude module: 'fabric-loader'
    }
    includeMod ('gg.essential:UniversalCraft:next-137-11700-fabric-SNAPSHOT') {
        exclude module: 'fabric-loader'
    }

    minecraft 'com.mojang:minecraft:1.17.1'
    mappings 'net.fabricmc:yarn:1.17.1+build.14:v2'

    modImplementation 'net.fabricmc:fabric-loader:0.11.6'
    modImplementation 'net.fabricmc.fabric-api:fabric-api:0.37.0+1.17'
    modImplementation 'net.fabricmc:fabric-language-kotlin:1.6.2+kotlin.1.5.20'
}

processResources {
    inputs.property 'mod_id', mod_id
    inputs.property 'mod_version', project.version
    inputs.property 'mod_name', mod_name
    inputs.property 'mod_description', mod_description

    filesMatching('fabric.mod.json') {
        expand(
                'mod_id': mod_id,
                'mod_version': project.version,
                'mod_name': mod_name,
                'mod_description': mod_description
        )
    }
}

shadowJar {
    archiveClassifier.set 'shadow'
}

// use shadow as input jar for remap
remapJar {
    dependsOn shadowJar
    input.fileValue shadowJar.outputs.files.singleFile

    archiveBaseName.set shadowJar.archiveBaseName.get()
    archiveVersion.set shadowJar.archiveVersion.get()
    archiveClassifier.set ''
}

compileKotlin {
    kotlinOptions {
        jvmTarget = '16'
    }
}