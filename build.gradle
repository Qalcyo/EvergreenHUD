/*
 * EvergreenHUD - A mod to improve on your heads-up-display.
 * Copyright (C) isXander [2019 - 2021]
 *
 * This program comes with ABSOLUTELY NO WARRANTY
 * This is free software, and you are welcome to redistribute it
 * under the certain conditions that can be found here
 * https://www.gnu.org/licenses/lgpl-2.1.en.html
 *
 * If you have any questions or concerns, please create
 * an issue on the github page that can be found here
 * https://github.com/isXander/EvergreenHUD
 *
 * If you have a private concern, please contact
 * isXander @ business.isxander@gmail.com
 */

import com.modrinth.minotaur.TaskModrinthUpload

plugins {
    // Languages
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.5.31'

    // Build
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id 'org.ajoberstar.grgit' version '4.1.0'

    // Publishing
    id "com.modrinth.minotaur" version "1.2.1" apply false

    // Development
    id "org.jetbrains.gradle.plugin.idea-ext" version "1.1" apply false
}

ext {
    prerelease = !version_prerelease.trim().isEmpty()
    gitHash = (grgit != null && grgit.head() != null ? grgit.head().abbreviatedId : null)
    versionStr = {
        def ver = "$version_major.$version_minor.$version_patch"

        // CI Builds Only
        def run_num = System.getenv('GITHUB_RUN_NUMBER')
        if (run_num != null)
            ver += "-build.${run_num}"

        if (prerelease)
            ver += "-pre.${version_prerelease}"

        return ver
    }()
    versionWithRev = {
        def ver = versionStr

        if (ext.gitHash) {
            ver += "-rev.${ext.gitHash}"
        }

        return ver
    }()
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'org.ajoberstar.grgit'
    apply plugin: 'org.jetbrains.gradle.plugin.idea-ext'

    version = versionStr
    group = 'dev.isxander.evergreenhud'

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    repositories {
        mavenCentral()
        maven { url = 'https://jitpack.io/' }
        maven { url = 'https://repo.sk1er.club/repository/maven-public' }
    }

    configurations {
        embed
        implementation.extendsFrom(embed)
    }

    jar {
        archiveBaseName.set("EvergreenHUD (${project.name}-${project.versionWithRev})")
        archiveVersion.set("")
        archiveClassifier.set("dev")

        exclude 'META-INF/INDEX.LIST', 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA', 'module-info.class'
    }

    shadowJar {
        archiveBaseName.set(jar.archiveBaseName.get())
        archiveClassifier.set('')
        archiveVersion.set('')

        configurations = [project.configurations.embed]
        duplicatesStrategy DuplicatesStrategy.EXCLUDE
    }

    compileKotlin {
        kotlinOptions {
            freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
            freeCompilerArgs += "-Xmulti-platform"
        }
    }

    if (project.parent != null && project.parent.name == "compat") {
        apply plugin: 'com.modrinth.minotaur'

        task publishModrinth(type: TaskModrinthUpload) {
            onlyIf {
                System.getenv("MODRINTH")
            }

            token = System.getenv("MODRINTH")
            projectId = "1yIQcc2b"
            versionNumber = project.version
            uploadFile = jar
        }
    }
}

import org.jetbrains.gradle.ext.*

idea.project.settings {
    copyright {
        useDefault = "EvergreenHUD"
        profiles {
            EvergreenHUD {
                notice = """
EvergreenHUD - A mod to improve on your heads-up-display.
Copyright (C) isXander [2019 - \$today.year]

This program comes with ABSOLUTELY NO WARRANTY
This is free software, and you are welcome to redistribute it
under the certain conditions that can be found here
https://www.gnu.org/licenses/lgpl-2.1.en.html

If you have any questions or concerns, please create
an issue on the github page that can be found here
https://github.com/isXander/EvergreenHUD

If you have a private concern, please contact
isXander @ business.isxander@gmail.com
"""
                keyword = "Copyright"
            }
        }
    }
    runConfigurations {
        "Build All"(Gradle) {
            taskNames = ["build"]
        }

        "Run 1.8"(Application) {
            mainClass = "GradleStart"
            moduleName = "EvergrenHUD.compat.forge-1.8.9.main"
            workingDirectory = rootProject.file("run").path
        }
        "Run 1.17"(Gradle) {
            taskNames = [":compat:fabric-1.17.1:runClient"]
        }
    }
}




