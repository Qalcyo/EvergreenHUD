plugins {
    // Languages
    id 'java'

    // APIs
    id 'net.minecraftforge.gradle.forge' version '8708bf3e0'
    id 'org.spongepowered.mixin' version '0.6-SNAPSHOT'

    // Build
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id 'io.freefair.lombok' version '6.0.0-m2'
    id 'org.ajoberstar.grgit' version '4.1.0'
}

def gitHash = (grgit != null ? grgit.head().abbreviatedId : null)
version = {
    def build_num = "0"
    def run_num = System.getenv('GITHUB_RUN_NUMBER')
    // CI builds only
    if (run_num != null)
        build_num = run_num

    String ver = "${mod_version}.${build_num}"

    if (!mod_prerelease.trim().isEmpty())
        ver += "-pre.${mod_prerelease}"

    return ver
}()
def versionWithGit = {
    String ver = project.version

    if (gitHash) {
        ver += "-rev.${gitHash}"
    }

    return ver
}()

group = 'dev.isxander'
archivesBaseName = 'evergreenhud'

targetCompatibility = sourceCompatibility = JavaVersion.VERSION_1_8
compileJava.options.encoding = 'UTF-8'

minecraft {
    version = '1.8.9-11.15.1.2318-1.8.9'
    runDir = 'run'
    mappings = 'stable_22'
    makeObfSourceJar = false

    replace '@BUILD_GIT_COMMIT_HASH@', (grgit != null ? grgit.head().abbreviatedId : '???????')
    replace '@BUILD_VER_NORMAL@', project.version
    replace '@BUILD_VER_WITH_GIT@', versionWithGit
    replace '@BUILD_MOD_VERSION@', mod_version
    replace '@BUILD_MOD_PRERELEASE@', (mod_prerelease.trim().isEmpty() ? null : mod_prerelease)

    clientRunArgs += '--tweakClass org.spongepowered.asm.launch.MixinTweaker'
    clientRunArgs += '--mixin mixins.evergreenhud.json'
}

configurations {
    include
    implementation.extendsFrom(include)
}

repositories {
    maven {
        name = 'SpongePowered'
        url = 'https://repo.spongepowered.org/maven/'
    }
    maven {
        name = 'JitPack'
        url = 'https://jitpack.io/'
    }
    maven {
        name = 'Sk1er Public'
        url = 'https://repo.sk1er.club/repository/maven-public/'
    }
}

dependencies {
    include 'gg.essential:loader-launchwrapper:1.1.1'
    implementation 'gg.essential:essential-1.8.9-forge:1328'

    include ('dev.isXander:XanderLib:1.1.7') {
        exclude module: 'Essential'
        exclude module: 'kotlin-stdlib-jdk8'
    }
    annotationProcessor 'org.spongepowered:mixin:0.7.11-SNAPSHOT'
}

mixin {
    disableRefMapWarning = true
    add sourceSets.main, 'mixins.evergreenhud.refmap.json'
}

String evergreenArchiveName = "EvergreenHUD (${versionWithGit})"

jar {
    archiveName = "${evergreenArchiveName}.jar"
    enabled = false

    manifest {
        attributes (
                'TweakClass': 'dev.isxander.evergreenhud.forge.EvergreenHUDTweaker',
                'MixinConfigs': 'mixins.evergreenhud.json',
                'TweakOrder': 0
        )
    }
}

processResources {
    inputs.property 'version', project.version
    inputs.property 'mcversion', project.minecraft.version

    filesMatching('mcmod.info') {
        expand (
                'version': project.version,
                'mcversion': project.minecraft.version
        )
    }

    rename '(.+_at.cfg)', 'META-INF/$1'
}

// Forge does not import resources properly.
task moveResources {
    doLast {
        ant.move file: "${buildDir}/resources/main",
                todir: "${buildDir}/classes/java"
    }
}

tasks.moveResources.dependsOn processResources
tasks.classes.dependsOn moveResources

shadowJar {
    archiveBaseName.set(evergreenArchiveName)
    archiveVersion.set('')
    archiveClassifier.set('')

    configurations = [project.configurations.include]
    duplicatesStrategy DuplicatesStrategy.EXCLUDE
}

reobf {
    shadowJar {
        mappingType = 'SEARGE'
    }
}

reobfJar.dependsOn tasks.shadowJar
