buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = "https://jitpack.io" }
        maven { url = "https://repo.spongepowered.org/maven/" }
    }
    dependencies {
        classpath 'com.github.ReplayMod:ForgeGradle:d1a7165:all'
        classpath 'dev.isXander:MixinGradle:0.6-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

minecraft {
    version = '1.8.9-11.15.1.2318-1.8.9'
    runDir = 'run'
    mappings = 'stable_22'
    makeObfSourceJar = false

    clientRunArgs += '--tweakClass org.spongepowered.asm.launch.MixinTweaker'
    clientRunArgs += '--mixin evergreenhud.forge10809.mixins.json'
}

repositories {
    maven {
        name = 'SpongePowered'
        url = 'https://repo.spongepowered.org/maven/'
    }
}

dependencies {
    embed (project(":core")) {
        exclude module: 'Elementa'
        exclude module: 'UniversalCraft'
    }
    embed 'gg.essential:Elementa:next-337-10809-SNAPSHOT'
    embed 'gg.essential:UniversalCraft:next-137-10809-SNAPSHOT'

    // not sure why but gradle ass
    embed 'club.chachy.kotlin-forge-api:forge-event-dsl:0.3.1'
    embed ('com.github.KevinPriv:KEventBus:86cf2e023e') {
        exclude module: 'kotlin-stdlib-jdk8'
    }
    embed 'org.reflections:reflections:0.9.12'
    embed ('com.squareup.okhttp3:okhttp:4.9.1') {
        exclude module: 'kotlin-stdlib'
        exclude module: 'kotlin-stdlib-common'
    }

    implementation 'org.spongepowered:mixin:0.7.11-SNAPSHOT'
    annotationProcessor 'org.spongepowered:mixin:0.7.11-SNAPSHOT'
}

mixin {
    disableRefMapWarning = true
    add sourceSets.main, 'evergreenhud.forge10809.mixins.refmap.json'
}

jar {
    manifest.attributes (
            'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
            'MixinConfigs': 'evergreenhud.forge10809.mixins.json'
    )
}

processResources {
    inputs.property 'mod_id', mod_id
    inputs.property 'mod_version', project.version
    inputs.property 'mod_name', mod_name
    inputs.property 'mod_description', mod_description

    filesMatching('mcmod.info') {
        expand(
                'mod_id': mod_id,
                'mod_version': project.version,
                'mod_name': mod_name,
                'mod_description': mod_description
        )
    }
}

// Forge does not import resources properly.
task moveResources {
    doLast {
        ant.move file: "${buildDir}/resources/main",
                todir: "${buildDir}/classes/java"
    }
}

tasks.moveResources.dependsOn processResources
tasks.classes.dependsOn moveResources

reobf {
    shadowJar {
        mappingType = 'SEARGE'
    }
}

reobfJar.dependsOn tasks.shadowJar

compileKotlin {
    kotlinOptions {
        jvmTarget = '1.8'
    }
}